#
#	Buy & Sell API 1.0.0
#	
#	Created for as a commission for PlayInABox.
#	Copyright © 2021 Baylor Henshaw <baylor@henshaw.me>
#
#	Written by Baylor Henshaw (https://baezor.com/)
#

#
#	Options
#

# Style Settings
options:
	
	prefix: &6Shop &8»
	color.1: &7
	color.2: &e
	color.3: &6

#
#	Buy & Sell API
#

# Shop Buy
function shop_buy(player: player, category: string, id: string, amount: number):
	set {_cost} to {_amount}*shop_getBuyPrice({_category}, {_id})
	{_cost} is set

	if {_player}'s balance >= {_cost}:
		remove {_cost} from {_player}'s balance
		play sound "entity.experience_orb.pickup" to {_player}
		give {_player} {_amount} of shop_getItem({_category}, {_id})
		message "{@prefix} {@color.1}You have just purchased {@color.2}%{_amount}%{@color.1} of {@color.2}%{_id}%{@color.1}!" to {_player}
	else:
		play sound "block.note_block.bass" to {_player}
		send action bar "&cYou cannot afford this!" to {_player}
		message "{@prefix} &cYou cannot afford this!" to {_player}

# Shop Sell
function shop_sell(player: player, category: string, id: string, amount: number):
	if {_player}'s inventory contains {_amount} of shop_getItem({_category}, {_id}):
		set {_cost} to {_amount}*shop_getSellPrice({_category}, {_id})
		{_cost} is set

		add {_cost} to {_player}'s balance
		play sound "entity.experience_orb.pickup" to {_player}
		remove {_amount} of shop_getItem({_category}, {_id}) from {_player}'s inventory
		message "{@prefix} {@color.1}You have just sold {@color.2}%{_amount}%{@color.1} of {@color.2}%{_id}%{@color.1} for &a$%{_cost}%{@color.1}!" to {_player}

	else:
		play sound "block.note_block.bass" to {_player}
		send action bar "&cYou don't have enough items!" to {_player}
		message "{@prefix} &cYou don't have enough items!" to {_player}
